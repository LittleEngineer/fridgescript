{-
    This file is part of FSAssembler.

    FSAssembler is free software: you can redistribute it and/or modify
    it under the terms of the GNU Lesser General Public License as published by
    the Free Software Foundation, either version 3 of the License, or
    (at your option) any later version.

    Foobar is distributed in the hope that it will be useful,
    but WITHOUT ANY WARRANTY; without even the implied warranty of
    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
    GNU Lesser General Public License for more details.

    You should have received a copy of the GNU Lesser General Public License
    along with FSAssembler.  If not, see <http://www.gnu.org/licenses/>
-}

comment "#" ;
comment "\'" ;
comment "//" ;
comment "--" ;
comment "/*" "*/" ;
comment "{-" "-}" ;

entrypoints Code ;

Main.		Code 		::= [Operation] ;

terminator Operation "" ;

OAddC.		Operation 	::= "add" Register "," Integer ;
OSubC.		Operation 	::= "sub" Register "," Integer ;

OLbl.		Operation	::= Ident ":" ;
OJmp.		Operation	::=	"jmp" Operand ;
OJb.		Operation	::=	"jb" Operand ;
OJbe.		Operation	::=	"jbe" Operand ;
OJe.		Operation	::=	"je" Operand ;
OJne.		Operation	::=	"jne" Operand ;
OJz.		Operation	::=	"jz" Operand ;
OJnz.		Operation	::=	"jnz" Operand ;
OCall.		Operation	::=	"call" Operand ;
ORet.		Operation	::=	"ret" ;

OPush.		Operation	::= "push" Operand ;
OPop.		Operation	::= "pop" Operand ;

OFadd.		Operation	::= "fadd" ;
OFaddp.		Operation	::= "faddp" ;
OFsub.		Operation	::= "fsub" ;
OFsubp.		Operation	::= "fsubp" ;
OFsubrp.	Operation	::= "fsubrp" ;
OFmulp.		Operation	::= "fmulp" ;
OFdivp.		Operation	::= "fdivp" ;
OFdivrp.	Operation	::= "fdivrp" ;

OFchs.		Operation	::= "fchs" ;
OFsin.		Operation	::= "fsin" ;
OFcos.		Operation	::= "fcos" ;
OFprem.		Operation	::= "fprem" ;
OFptan.		Operation	::= "fptan" ;
OFpatan.	Operation	::= "fpatan" ;
OFsqrt.		Operation	::= "fsqrt" ;
OFrndint.	Operation	::= "frndint" ;
OFscale.	Operation	::= "fscale" ;
OFtxmo.		Operation	::= "f2xm1" ;
OFyltx.		Operation	::= "fyl2x" ;
OFyltxpo.	Operation	::= "fyl2xp1" ;

OFcomi.		Operation	::= "fcomi" ;
OFcmovb.	Operation	::= "fcmovb" ;
OFcmovbe.	Operation	::= "fcmovbe" ;
OFcmove.	Operation	::= "fcmove" ;
OFcmovnb.	Operation	::= "fcmovnb" ;
OFcmovnbe.	Operation	::= "fcmovnbe" ;
OFcmovne.	Operation	::= "fcmovne" ;
OFcmovnu.	Operation	::= "fcmovnu" ;
OFcmovu.	Operation	::= "fcmovu" ;

OFld.		Operation	::= "fld" Operand ;
OFild.		Operation	::= "fild" Operand ;
OFldt.		Operation	::= "fld" "st(0)" ;
OFldn.		Operation	::= "fld" "st(1)" ;
OFldz.		Operation	::= "fldz" ;
OFldo.		Operation	::= "fld1" ;
OFldpi.		Operation	::= "fldpi" ;
OFldlte.	Operation	::= "fldl2e" ;
OFldlnt.	Operation	::= "fldln2" ;
OFldlgt.	Operation	::= "fldlg2" ;

OFst.		Operation	::= "fst" Operand ;
OFist.		Operation	::= "fist" Operand ;
OFstp.		Operation	::= "fstp" Operand ;
OFistp.		Operation	::= "fistp" Operand ;
OFstpt.		Operation	::= "fstp" "st(0)" ;

OFfree. 	Operation	::= "ffree" "st(0)" ;
OFfreea.	Operation	::= "ffree" "st(1)" ;
OFfreeb.	Operation	::= "ffree" "st(2)" ;
OFfreec.	Operation	::= "ffree" "st(3)" ;
OFfreed.	Operation	::= "ffree" "st(4)" ;
OFfreee.	Operation	::= "ffree" "st(5)" ;
OFfreef.	Operation	::= "ffree" "st(6)" ;
OFfreeg.	Operation	::= "ffree" "st(7)" ;

OFxchg.		Operation	::= "fxch" ;

OFincstp.	Operation	::= "fincstp" ;
OFdecstp.	Operation	::= "fdecstp" ;

REax.		Register	::= "eax" ;
REcx.		Register	::= "ecx" ;
REdx.		Register	::= "edx" ;
REbx.		Register	::= "ebx" ;
REsp.		Register	::= "esp" ;
REbp.		Register	::= "ebp" ;
REsi.		Register	::= "esi" ;
REdi.		Register	::= "edi" ;

OReg.		Operand		::= Register ;
OHex.		Operand		::= HexConstant ;
OLab.		Operand		::= Ident ;
OLitAdd.	Operand		::= "[" HexConstant "]" ;
ORegAdd.	Operand		::= "[" Register "]" ;
ORelAddP.	Operand		::= "[" Register "+" Integer "]" ;
ORelAddS.	Operand		::= "[" Register "-" Integer "]" ;

token HexConstant '0' ('x'|'X') (digit | ["abcdef"] | ["ABCDEF"])+ ;